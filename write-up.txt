lab9 write-up
By Tongxi(Tom) Liu, Jessie Zeng
Mar 4th, 2022


1. Documentation of what you changed in your grammar to do MRS refinement. Include IGT to illustrate.

Fix lab 8:

asrt-assertive := mood. -> asrt := mood.

locative-pp-phrase:
    1. reduce ambiguity by removing SPEC < >
    2. PRED "_loc_p_rel"

TODO: Transfer rules, Trigger rules

2. Documentation of what you changed in your grammar to cut back on overgeneration. Include IGT or pointers to the MMT sentences to illustrate.
IGT for possessive marker:
    # 1
    húy=ki garí túm-í
    Dogs=POSS cars sleep-NHYP
    'The dog's cars sleeps'

In lab 8, we discovered our poss =ki doesn't work. We went back to customization system and made poss as specifier instead of modifier by adding two rules head-spec-2 and poss-unary-1:
    head-spec-2 := head-spec-phrase-2.
    poss-unary-1 := poss-unary-phrase-1.

    poss-unary-phrase-1 := poss-unary-phrase &
      [ ARGS < [ SYNSEM.LOCAL.CAT.HEAD.POSSESSOR possessor-1 ] >,
        SYNSEM.LOCAL.CAT [ HEAD.SPEC-INIT +,
                           VAL.SPEC.FIRST.LOCAL.CAT.POSSESSUM nonpossessive ] ].

    poss-unary-phrase := basic-unary-phrase &
      [ SYNSEM [ NON-LOCAL #nonloc,
                 LOCAL [ CONT.HOOK #hook,
                         CAT [ HEAD det &
                                    [ POSSESSOR possessor ],
                               VAL [ SPR < >,
                                     COMPS < >,
                                     SUBJ < >,
                                     SPEC < [ LOCAL [ CAT [ VAL.COMPS < >,
                                                            HEAD noun &
                                                                 [ PRON - ] ],
                                                      CONT.HOOK #hook &
                                                                [ INDEX #possessum &
                                                                        [ COG-ST uniq-id ],
                                                                  LTOP #lbl ] ] ] > ] ] ] ],
        C-CONT [ RELS.LIST < arg12-ev-relation &
                             [ PRED "poss_rel",
                               LBL #lbl,
                               ARG1 #possessum,
                               ARG2 #possessor ],
                             quant-relation &
                             [ PRED "exist_q_rel",
                               ARG0 #possessum,
                               RSTR #harg ] >,
                 HCONS.LIST < qeq &
                              [ HARG #harg,
                                LARG #lbl ] >,
                 ICONS.LIST < > ],
        ARGS < [ SYNSEM [ LOCAL [ CAT [ VAL [ SPR < >,
                                              COMPS < >,
                                              SUBJ < >,
                                              SPEC < > ],
                                        HEAD +np ],
                                  CONT.HOOK.INDEX #possessor ],
                          NON-LOCAL #nonloc ] ] > ].

    head-spec-phrase-2 := head-final & basic-head-spec-phrase &
      [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD.SPEC-INIT +,
                                        VAL.SPEC < [ LOCAL.CAT.POSSESSUM #poss ] > ],
        HEAD-DTR.SYNSEM.LOCAL.CAT.POSSESSUM #poss ].

head-spec-2 forms "húy=ki garí" as a NP, and poss-unary-1 builds a non-branching D node for bare-NP "húy=ki".
So for #1, the tree is S -> NP(head-spec-2) VP, NP -> D(poss-unary-1) N
However, we found various problems with this structure:
It can parse ungrammatical sentence like:
    #2
    húy=nə  garí túm-í
    dogs=AGN cars sleep-NHYP
    '*The dogs cars sleeps'

The sentence #2 has identical MRS as #1. Because poss-unary-1 can take any Bare-NP to form a D node, we need to find a way to constraint it to only take NP with CASE poss-case.

Therefore, we modified the ARGS in poss-unary-phrase to have HEAD noun & [ CASE poss-case]. It correctly ruled out the case in #2 and only parsed sentence #1. However, when we generate #1 in LKB, we can get ungrammatical sentence like follows:
    #3
    húy  garí túm-í
    dogs cars sleep-NHYP
    '*The dogs cars sleeps'

In #3, because the CASE for húy is underspecified (null case), it can take poss-case and form a NP to satisfy the requirement for poss-unary-1. Therefore, to rule out this situation, we came up with another way: we specified that poss-unary-1 can only take a determiner as its daughter, and changed noun-possessor-pc_lrt1-suffix =ki to be a determiner. We think it might not be the legit way to do since the structure of tree is weird, but this approach can rule out ambiguities and have correct MRS for MT to work.

Here are the TDLs:
We changed possessor-lex-rule-1 to be det POS.
    possessor-lex-rule-1 := head-change-only-lex-rule &
      [ SYNSEM.LOCAL.CAT.HEAD det &
                              [
                                POSSESSOR possessor-1 ] ].

We modified poss-unary-phrase to take a D node as input:
    poss-unary-phrase := basic-unary-phrase &
      [ SYNSEM [ NON-LOCAL #nonloc,
                 LOCAL [ CONT.HOOK #hook,
                         CAT [ HEAD det &
                                    [ POSSESSOR possessor ],
                               VAL [ SPR < >,
                                     COMPS < >,
                                     SUBJ < >,
                                     SPEC < [ LOCAL [ CAT [ VAL.COMPS < >,
                                                            HEAD det &
                                                                 [ PRON - ] ],
                                                      CONT.HOOK #hook &
                                                                [ INDEX #possessum &
                                                                        [ COG-ST uniq-id ],
                                                                  LTOP #lbl ] ] ] > ] ] ] ],
        C-CONT [ RELS.LIST < arg12-ev-relation &
                             [ PRED "poss_rel",
                               LBL #lbl,
                               ARG1 #possessum,
                               ARG2 #possessor ],
                             quant-relation &
                             [ PRED "exist_q_rel",
                               ARG0 #possessum,
                               RSTR #harg ] >,
                 HCONS.LIST < qeq &
                              [ HARG #harg,
                                LARG #lbl ] >,
                 ICONS.LIST < > ],
        ARGS < [ SYNSEM [ LOCAL [ CAT [ VAL [
                                              COMPS < >,
                                              SUBJ < >,
                                              SPEC < > ],
                                        HEAD det ],
                                  CONT.HOOK.INDEX #possessor ],
                          NON-LOCAL #nonloc ] ] > ] .

Now, the head-spec-2 still cannot form a NP because it inherits from basic-head-spec-phrase-super, which regulates the HEAD of first and second daughter should match. We defined a new basic-head-spec-phrase-custom-super which removed this constraint, and make head-spec-2 inherit from it:
    basic-head-spec-phrase-custom-super := binary-nonloc-phrase & phrasal &
                  binary-headed-phrase &
      [ INFLECTED infl-satisfied,
        SYNSEM [ LOCAL.CAT [ WH [ OR < #or1, #or2 > ],
                             VAL [ SUBJ #subj,
                                   COMPS #spcomps,
                                   SPR #spr,
                                   SPEC #spec ],
                             POSTHEAD #ph,
                             HC-LIGHT - ],
                  MODIFIED #modif ],
        HEAD-DTR [ INFLECTED infl-satisfied,
                   SYNSEM [ LOCAL [ CAT [ WH #or1,
                                          VAL [ SUBJ #subj,
                                                COMPS olist & #comps,
                                                SPR < #synsem &
                                                      canonical-synsem . #spr >,
                                                SPEC #spec ],
                                          POSTHEAD #ph ],
                                    CONT.HOOK #hdhook ],
                            MODIFIED #hmodif ] ],
        NON-HEAD-DTR.SYNSEM #synsem &
         [ LOCAL [ CAT [ WH #or2, VAL [ SPEC < [ LOCAL [ CAT [
                                                      VAL.COMPS #comps ],
                                                CONT.HOOK #hdhook ],
                                        MODIFIED #hmodif ] >,
                               COMPS #spcomps ] ] ],
           MODIFIED #modif ],
        C-CONT [ RELS.LIST < >, HCONS.LIST < >, ICONS.LIST < > ] ].

    head-spec-phrase-2 := head-final & basic-head-spec-phrase-custom-super &
      [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD.SPEC-INIT +,
                                        VAL.SPEC < [ LOCAL.CAT.POSSESSUM #poss ] > ],
        HEAD-DTR.SYNSEM.LOCAL.CAT.POSSESSUM #poss ].

Now, the case in #2 and #3 are correctly ruled out, and #1 can be parsed correctly.


3. For any residual realization ambiguity (multiple outputs for one input), characterize the sources (e.g. free word order).
Our realization ambiguity comes from X phenomena:
    1. Mood markers: verbs can have either ASRT or NHYP for verbal sentences
        Húy túm-í
        dog sleep-NHYP

        Húy túm-e
        dog sleep-ASRT
    2. Number: pronouns have singular, dual, and plural forms
        Ə́y nə́bani tan-í
        1P 2Pdual chase-NHYP

        Ə́y nə́khóy tan-í
        1P 2PPL chase-NHYP

        Ə́y nə́ŋ tan-í
        1P 2PSG chase-NHYP
    3. Different forms of possessive markers:
        Ə́y=ki   húy túm-í
        1P=POSS dog slee-NHYP

        Í-húy túm-í
        1P-dog sleep-NHYP

eng:
[ LTOP: h0 INDEX: e1 [ e TENSE: present ASPECT: aspect SF: prop-or-ques ] RELS: < [ "exist_q_rel"<-1:
-1> LBL: h2 ARG0: x3 [ x PER: third NUM: sg COG-ST: uniq+fam+act ] RSTR: h4 BODY: h5 ]  [ "_dog_n_rel
"<-1:-1> LBL: h6 ARG0: x3 ]  [ "exist_q_rel"<-1:-1> LBL: h7 ARG0: x8 [ x PER: third NUM: sg COG-ST: u
niq+fam+act ] RSTR: h9 BODY: h10 ]  [ "poss_rel"<-1:-1> LBL: h11 ARG0: e12 [ e TENSE: tense ASPECT: a
spect SF: iforce ] ARG1: x8 ARG2: x3 ]  [ "_car_n_rel"<-1:-1> LBL: h11 ARG0: x8 ]  [ "_sleep_v_rel"<-
1:-1> LBL: h13 ARG0: e1 ARG1: x8 ] > HCONS: < h0 qeq h13 h4 qeq h6 h9 qeq h11 > ICONS: < > ]

mni:
[ LTOP: h0 INDEX: e1 [ e ASPECT: aspect SF: prop-or-ques ] RELS: < [ "poss_rel"<-1:-1> LBL: h2 ARG0:
e3 [ e ASPECT: aspect SF: iforce ] ARG1: x4 [ x PER: third NUM: number COG-ST: uniq-id ] ARG2: x5 [ x
 PER: third NUM: number COG-ST: cog-st ] ]  [ "exist_q_rel"<-1:-1> LBL: h6 ARG0: x4 RSTR: h7 BODY: h8
 ]  [ "_dog_n_rel"<-1:-1> LBL: h9 ARG0: x5 ]  [ "_car_n_rel"<-1:-1> LBL: h2 ARG0: x4 ]  [ "_sleep_v_r
el"<-1:-1> LBL: h10 ARG0: e1 ARG1: x11 [ x PER: person NUM: number COG-ST: cog-st ] ] > HCONS: < h0 q
eq h10 h7 qeq h2 > ICONS: < > ]

4. Documentaiton of your MT coverage for both eng and sje as source languages.
Worked ENG sentences:
    1. Dogs sleep ->
        Húy túm-í
        Húy túm-e

    2. Dogs chase cars ->
        Húy garí tan-e
        Húy garí tan-í

    3. I chase you ->
        Ə́y nə́ŋ tan-e
        Ə́y nə́khóy tan-e
        Ə́y nə́bani tan-í
        Ə́y nə́bani tan-e
        Ə́y nə́khóy tan-í
        Ə́y nə́ŋ tan-í

    4. Dogs eat ->
        Húy čá-í
        Húy čá-e

    5. The dogs dont chase cars ->
        Húy garí tan-tə-e

    15. The dogs are hungry ->
        Húy hungryInMNI-í
        Húy hungryInMNI-e

    16. The dogs are in the park ->
        Húy bəzar=tə =ni

    17. The dogs are the cats ->
        Húy hə́wdóŋ =ni

    18. The dog s car sleeps ->
        Húy=ki garí túm-í
        Húy=ki garí túm-e

    19. My dog sleeps ->
        Ə́y=ki húy túm-í
        Í-húy túm-í
        Ə́y=ki húy túm-e
        Í-húy túm-e

Worked SJE translations:
    1. Bednaga urri ->
        Húy túm-í
        Húy túm-e

    2. Bednaga haoskaoli bijlajd ->
        Húy garí tan-e
        Húy garí tan-í

    3. Maon haoskaolav duv ->
        Ə́y nə́ŋ tan-e
        Ə́y nə́ŋ tan-í

    4. Bednaga burri ->
        Húy čá-í
        Húy čá-e

    5. Bednaga eh haoskaolao bijlajd ->
        Húy garí tan-tə-e

    18. Bednaga bijjla aorraa ->
        Húy=ki garí túm-í
        Húy=ki garí túm-e

    19. Muv bednaga urri ->
        Ə́y=ki húy túm-í
        Í-húy túm-í
        Ə́y=ki húy túm-e
        Í-húy túm-e

5.
How many items parsed?
  Before improvements:
  	test corpus: 3/1784 items parsed;
  	test suite: 28/38.
  After improvements:
  	test corpus: 15/1784 items parsed;
  	test suite: all 38 items parsed.

What is the average number of parses per parsed item?
  Before improvements:
  	test corpus: 1.33 parses on average;
  	test suite: 1.25 parses on average.
  After improvements:
  	test corpus: 1.2 parses on average;
  	test suite: 1.26 parses on average.

How many parses did the most ambiguous item receive?
  Before improvements:
  	test corpus: 2 parses;
  	test suite: 2 parses.
  After improvements:
  	test corpus: 3 parses;
  	test suite: 2 parses;

What NEW sources of ambiguity can you identify?
  No new sources of ambiguity found.